plugins {
    id 'com.android.application'
}

android {
    namespace 'com.jieli.healthaide'
    compileSdk 36
    buildToolsVersion '36.0.0'

    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    defaultConfig {
        applicationId "com.jieli.healthaide"
        minSdk 21
        targetSdk 35
        versionCode 909
        versionName "1.1.0"
        archivesBaseName = getAppName("JLHealthAide", versionName, versionCode)

        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "user_agreement_url", "https://cam.jieliapp.com/app/app.user.service.protocol.html"
        resValue "string", "app_privacy_policy", "https://cam.jieliapp.com/app/healthaide_app_privacy_policy.html"
        resValue "string", "icp_number", "粤ICP备18069041号-3A"

        //指定room.schemaLocation生成的文件路径
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            // 设置支持的SO库架构
            abiFilters 'armeabi-v7a', 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "IFLYTEK_APP_ID", getProps("IFLYTEK_APP_ID"))
            buildConfigField("String", "IFLYTEK_API_KEY", getProps("IFLYTEK_API_KEY"))
            buildConfigField("String", "IFLYTEK_API_SECRET", getProps("IFLYTEK_API_SECRET"))
        }
        debug {
            signingConfig signingConfigs.debug
            buildConfigField("String", "IFLYTEK_APP_ID", getProps("IFLYTEK_APP_ID"))
            buildConfigField("String", "IFLYTEK_API_KEY", getProps("IFLYTEK_API_KEY"))
            buildConfigField("String", "IFLYTEK_API_SECRET", getProps("IFLYTEK_API_SECRET"))
        }
    }
    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        dataBinding = true
        viewBinding = true
        buildConfig = true
    }


    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

static def getSystemTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("GMT+08:00"))
}

static def getAppName(String prefix, String versionName, int versionCode) {
    if (versionName.contains("beta")) {
        return "${prefix}_V${versionName}_${versionCode}_${getSystemTime()}"
    }
    return "${prefix}_V${versionName}_${versionCode}"
}

def getProps(String propName) {
    def propsFile = rootProject.file('local.properties')
    if (propsFile.exists()) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        return props.getProperty(propName, "\"\"")
    } else {
        return "\"\""
    }
}

dependencies {

    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation fileTree(include: ['*.aar'], dir: 'libs')

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1'
    implementation 'androidx.navigation:navigation-fragment:2.3.5'
    implementation 'androidx.navigation:navigation-ui:2.3.5'
    implementation 'androidx.activity:activity:1.11.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'com.kyleduo.switchbutton:library:2.0.0'


    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.cardview:cardview:1.0.0'

    //RecyclerView通用适配器模板库
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:3.0.4'
    //刷新布局核心
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'

    //图表库
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    //Room持久性库
    implementation "androidx.room:room-runtime:2.3.0"
    annotationProcessor "androidx.room:room-compiler:2.3.0"
    //圆形ImageView控件
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    //图片加载库
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //网络库
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation("com.squareup.okhttp3:okhttp:4.10.0")
    implementation("com.squareup.okhttp3:logging-interceptor:4.10.0")
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.10.0'
    implementation 'com.squareup.okhttp3:mockwebserver:4.10.0'

    implementation 'com.github.mcxtzhang:SwipeDelMenuLayout:V1.2.5'

    implementation 'com.belerweb:pinyin4j:2.5.0'
    //扫描二维码库
    implementation 'com.github.jenly1314.WeChatQRCode:opencv:2.4.0'

    implementation 'com.github.jenly1314.WeChatQRCode:opencv-armv7a:2.4.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-armv64:2.4.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-x86:2.4.0'
    implementation 'com.github.jenly1314.WeChatQRCode:opencv-x86_64:2.4.0'

    implementation 'com.github.jenly1314.WeChatQRCode:wechat-qrcode:2.4.0'
    implementation 'com.github.jenly1314.WeChatQRCode:wechat-qrcode-scanning:2.4.0'

    //json解析库
    implementation 'com.google.code.gson:gson:2.13.1'

    //裁剪图片库
//    implementation 'com.github.yalantis:ucrop:2.2.6-native'

    implementation project(path: ':wheelview')

    implementation 'io.michaelrocks:libphonenumber-android:8.12.21'
    /*   implementation 'com.googlecode.libphonenumber:carrier:1.62'
       implementation 'com.googlecode.libphonenumber:geocoder:2.72'*/
    //
    //日历库
    implementation 'com.haibin:calendarview:3.7.1'
    //权限库
    implementation "com.github.permissions-dispatcher:permissionsdispatcher:4.9.2"
    annotationProcessor "com.github.permissions-dispatcher:permissionsdispatcher-processor:4.9.2"
    //支付宝SDK
    implementation 'com.alipay.sdk:alipaysdk-android:+@aar'

    //implementation 'com.google.firebase:firebase-crashlytics-buildtools:3.0.6'

    //MQTT库
    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
//    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'
    //json解析
    implementation 'com.alibaba:fastjson:1.2.83'
    //通用文本工具
    implementation 'org.apache.commons:commons-text:1.9'
    //Lottie动画
    implementation 'com.airbnb.android:lottie:5.2.0'
}